plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

android {
    namespace 'ru.panmin.lesson9'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        signingConfig signingConfigs.debug

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            File releaseSignFile = rootProject.file('release_keystore.properties')
            if (!releaseSignFile.exists()) {
                releaseSignFile = rootProject.file('debug_keystore.properties')
            }
            Properties releaseSignProperties = new Properties()
            if (releaseSignFile.exists()) {
                releaseSignProperties.load(new FileInputStream(releaseSignFile))
            }
            storeFile file(releaseSignProperties['keystore'])
            storePassword releaseSignProperties['storePassword']
            keyAlias releaseSignProperties['keyAlias']
            keyPassword releaseSignProperties['keyPassword']
        }

        debug {
            File releaseSignFile = rootProject.file('debug_keystore.properties')
            Properties releaseSignProperties = new Properties()
            if (releaseSignFile.exists()) {
                releaseSignProperties.load(new FileInputStream(releaseSignFile))
            }
            storeFile file(releaseSignProperties['keystore'])
            storePassword releaseSignProperties['storePassword']
            keyAlias releaseSignProperties['keyAlias']
            keyPassword releaseSignProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        internal {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable true
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            debuggable true
        }
    }


    flavorDimensions = ["version"]
    productFlavors {
        app1 {
            applicationId "ru.panmin.lessoneight"

            resValue "string", "app_name", "App1"

            buildConfigField 'String', 'MAPKIT_API_KEY', '"de14f28a-ae36-47cc-920a-7ae8ddb1442b"'

            manifestPlaceholders = [
                    manifesValue1: "ыыы",
                    manifesValue2: "ааа"
            ]

            dimension "version"
        }

        app2 {
            applicationId "ru.panmin.lessoneight"

            resValue "string", "app_name", "App2"

            buildConfigField 'String', 'MAPKIT_API_KEY', '"beeb073a-6b6b-4b7c-bbce-58186baa9c4a"'

            manifestPlaceholders = [
                    manifesValue1: "ооо",
                    manifesValue2: "иии"
            ]

            dimension "version"
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        buildConfig true
        viewBinding = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    //== firebase ==//
    implementation platform('com.google.firebase:firebase-bom:32.6.0')
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    //== mapkit ==//
    implementation 'com.yandex.android:maps.mobile:4.4.0-lite'
}